SIDE

!! INPUTS

l=ARG1


!/batch,list
/PREP7
/TITLE, sideways
 
! by allan liu 1175510

ET,1,PLANE53

MP,MURX,1,1 !air
MP,MURX,2,1.05 !magnet1 bottom magnet
MP,MGYY,2,985244.9 
MP,MURX,3,1.05 !magnet2 top magnet
MP,MGYY,3,-985244.9

!l=0.06		!magnet 2's sidewise deviation

X=0.22		!airgap width
Y=0.192		!airgap height
c=0.1           !left side to magnet1
a=0.01		!magnet half width
b=0.004		!magnet half height
m=0.08		!bottom to magnet1
d=0.016 	!distance between edges of magnet 1 and 2


RECTNG,0,X,0,Y  	!air gap
RECTNG,c,c+2*a,m,m+2*b  !magnet1
RECTNG,c+l,c+l+2*a,m+2*b+d,m+4*b+d   !magnet2


ASEL,S,AREA,,2		!selecting bottom magnet

AATT,2,,1 	        !assigning material 2 to bottom magnet

ASEL,S,AREA,,3		!selecting top magnet

AATT,3,,1		!assigning material 3 to top magnet

ASEL,ALL
ASBA,1,ALL,,,KEEP       !subtract all from air
ASEL,U,AREA,,2,3	!unselect the areas exept for airgap
AATT,1,,1

!Boundary Conditions

ALLSEL
LSEL,S,EXT   ! Select exterior nodes
DL,ALL,,AZ,0 ! defining the exterior boundaries

!Mesh:

ALLSEL
AESIZE,ALL,0.003
AMESH,ALL

!Apply force flags:

ESEL,S,MAT,,3       !Selecting material 3 top magnet
CM,mag1,ELEM
FMAGBC,'mag1'       !flagging magnet3 top magnet

FINISH  
ALLSEL
/SOL
MAGSOLV,2
FINISH


/POST1  
PLF2D
/com,
fmagsum,'mag1'
torqsum,'mag1' 

ESEL,S,MAT,,3  ! Select floating magnet elements
NSEL,S,EXT         ! Select exterior nodes of the floating magnet
ESLN               ! Select elements touching selected nodes
!  (ie outer magnet elements and touching air elements)
ESEL,U,MAT,,3  ! Unselect elements made of magnet
SSUM               ! Sum all forces held in these elements to get the overall forces on the magnet


! READ MAXWELL STRESS TENSOR FORCES

etab,fmgx1,fmag,x
etab,fmgy1,fmag,y
etab,fmgz1,fmag,z

! READ centroids from global origin

etab,cntrdx,cent,x
etab,cntrdy,cent,y
etab,cntrdz,cent,z

! Create the lever arm for each element centroid wrt centre of magnet

SADD,LEVX,cntrdx,,1,0,-(c+a+l)
SADD,LEVY,cntrdy,,1,0,-(m+3*b+d)
SADD,LEVZ,cntrdz,,0,0,0

! CALCULATE TORQUES (VIRTUAL WORK)

vcross,trqxvw,trqyvw,trqzvw,LEVX,LEVY,LEVZ,fvw_x,fvw_y,fmgz1


! CALCULATE TORQUES (MAXWELL STRESS TENSOR)

vcross,trqxmx,trqymx,trqzmx,LEVX,LEVY,LEVZ,fmgx1,fmgy1,fmgz1

SSUM

*dim,ff,ARRAY,6    ! Define a 8x1 array in which to store the forces
*get,ff(1),ssum,fmx_x ! Store Maxwell stress force
*get,ff(2),ssum,fvw_x ! Store Virtual work force
*get,ff(3),ssum,fmx_y ! 
*get,ff(4),ssum,fvw_y !
*get,ff(5),ssum,trqzmx   !store maxwell torque about centre of magnet
*get,ff(6),ssum,trqzvw   !store virtual work torque about centre of magnet


/OUTPUT,'sidevalues','ansys',,APPEND ! Save the data at the end of the file

*VWRITE,ff(1),ff(2),ff(3),ff(4),ff(5),ff(6)
(F10.3) ! Fortran number format for the vwrite numbers

/OUTPUT

/EOF














